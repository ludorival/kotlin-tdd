plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.20'
    id 'maven-publish'
    id 'org.jetbrains.kotlinx.kover' version '0.9.2'
    id 'io.github.gradle-nexus.publish-plugin' version "2.0.0"
    id 'com.palantir.git-version' version '3.4.0'
    id 'org.jreleaser' version '1.20.0'
}

group 'io.github.ludorival'
version gitVersion().replace(".dirty", "-SNAPSHOT")

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility(JavaVersion.VERSION_1_8)
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kotlin-tdd'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Kotlin TDD'
                description = 'Interface to use the Test Driven Design paradigm with internal state management'
                url = 'https://github.com/ludorival/kotlin-tdd'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ludorival/kotlin-tdd/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ludorival'
                        name = 'Ludovic Dorival'
                        email = 'ludorival@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ludorival/kotlin-tdd.git'
                    developerConnection = 'scm:git:ssh://github.com/ludorival/kotlin-tdd.git'
                    url = 'https://github.com/ludorival/kotlin-tdd'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("$buildDir/staging-deploy")
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
repositories {
    mavenCentral()
}
test {
    useJUnitPlatform()
}
kover.reports.verify.rule {
    minBound(93)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
}

jreleaser {
    project {
        name = 'kotlin-tdd'
        description = 'A Kotlin library for Test-Driven Development'
        longDescription = '''
            A Kotlin library that provides utilities and patterns 
            for practicing Test-Driven Development effectively
        '''
        authors = ['Ludovic Dorival']
        license = 'MIT License'
        copyright = 'Copyright (c) 2024 Ludovic Dorival'
        links {
            homepage = 'https://github.com/ludorival/kotlin-tdd'
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    applyMavenCentralRules = true
                    password = System.getenv('MAVEN_CENTRAL_PASSWORD')
                    username = System.getenv('MAVEN_CENTRAL_USERNAME')
                    active = 'ALWAYS'
                    url = "https://central.sonatype.com/api/v1/publisher"
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
