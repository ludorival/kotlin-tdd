plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'maven-publish'
    id 'org.jetbrains.kotlinx.kover' version '0.4.4'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'com.palantir.git-version' version '0.15.0'
    id 'org.jreleaser' version '1.15.0'
}

group 'io.github.ludorival'
version gitVersion().replace(".dirty", "-SNAPSHOT")

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility(JavaVersion.VERSION_1_8)
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kotlin-tdd'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Kotlin TDD'
                description = 'Interface to use the Test Driven Design paradigm with internal state management'
                url = 'https://github.com/ludorival/kotlin-tdd'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ludorival/kotlin-tdd/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ludorival'
                        name = 'Ludovic Dorival'
                        email = 'ludorival@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ludorival/kotlin-tdd.git'
                    developerConnection = 'scm:git:ssh://github.com/ludorival/kotlin-tdd.git'
                    url = 'https://github.com/ludorival/kotlin-tdd'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("$buildDir/staging-deploy")
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
repositories {
    mavenCentral()
}
test {
    useJUnitPlatform()
}

tasks.koverVerify {
    rule {
        name = "Minimal line coverage rate in percents"
        bound {
            minValue = 93
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    applyMavenCentralRules = true
                    password = findProperty('maven.password') ?: System.getenv('MAVEN_CENTRAL_PASSWORD')
                    username = findProperty('maven.username') ?: System.getenv('MAVEN_CENTRAL_USERNAME')
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local/'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
