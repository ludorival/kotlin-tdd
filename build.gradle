plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'maven-publish'
    id 'org.jetbrains.kotlinx.kover' version '0.4.4'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'com.palantir.git-version' version '0.12.3'
}

group 'io.github.ludorival'
version gitVersion()

java {
    withJavadocJar()
    withSourcesJar()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kotlin-tdd'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Kotlin TDD'
                description = 'Interface to use the Test Driven Design paradigm with internal state management'
                url = 'https://github.com/ludorival/kotlin-tdd'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ludorival/kotlin-tdd/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ludorival'
                        name = 'Ludovic Dorival'
                        email = 'ludorival@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ludorival/kotlin-tdd.git'
                    developerConnection = 'scm:git:ssh://github.com/ludorival/kotlin-tdd.git'
                    url = 'https://github.com/ludorival/kotlin-tdd'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("maven.username") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("maven.password") ?: System.getenv("OSSRH_GPG_SECRET_KEY_PASSWORD")
            }

        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
nexusPublishing {
    repositories {
        sonatype {
            username = findProperty("maven.username") ?: System.getenv("OSSRH_USERNAME")
            password = findProperty("maven.password") ?: System.getenv("OSSRH_GPG_SECRET_KEY_PASSWORD")
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/ludorival/kotlin-tdd"
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}
test {
    useJUnitPlatform()
}

tasks.koverVerify {
    rule {
        name = "Minimal line coverage rate in percents"
        bound {
            minValue = 93
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}
